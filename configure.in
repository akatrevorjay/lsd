#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.
AC_PREREQ(2.61)
#
# Change the version number below after doing a public release.
# The version in git should reflect the *next* version planned.
# Version must be MAJOR.MINOR.PATCH otherwise things will break.
#
AC_INIT([libshuppan],[0.0.1],[victor@iso3103.net])

AC_CONFIG_AUX_DIR(config)
AC_CONFIG_MACRO_DIR(config)
AM_CONFIG_HEADER(src/platform.h)
AM_INIT_AUTOMAKE(tar-ustar)

# This defines PACKAGE_VERSION_... in src/platform.h
PV_MAJOR=`echo $PACKAGE_VERSION | cut -d . -f 1`
PV_MINOR=`echo $PACKAGE_VERSION | cut -d . -f 2`
PV_PATCH=`echo $PACKAGE_VERSION | cut -d . -f 3`
AC_DEFINE_UNQUOTED([PACKAGE_VERSION_MAJOR],[$PV_MAJOR],
    [LIBSHUPPAN major version])
AC_DEFINE_UNQUOTED([PACKAGE_VERSION_MINOR],[$PV_MINOR],
    [LIBSHUPPAN minor version])
AC_DEFINE_UNQUOTED([PACKAGE_VERSION_PATCH],[$PV_PATCH],
    [LIBSHUPPAN patchlevel])
# This lets us use PACKAGE_VERSION in Makefiles
AC_SUBST(PACKAGE_VERSION)

#
# Libtool -version-info (ABI version)
#
# Currently 0.0.1 ("prealpha"). Don't change this unless you
# know exactly what you're doing and have read and understand
# http://www.gnu.org/software/libtool/manual/html_node/Updating-version-info.html
#
# libshuppan -version-info
LTVER="0:0:1"
AC_SUBST(LTVER)

# Capture c flags
LIBSHUPPAN_ORIG_CPPFLAGS="${CPPFLAGS:-none}"

# Checks for programs.
AC_LANG(C++)
AC_PROG_CC
AC_PROG_CXX
AC_PROG_LIBTOOL
AC_LIBTOOL_CXX
AC_PROG_SED
AC_PROG_AWK

AC_SUBST(LIBTOOL_DEPS)
AC_LTDL_DLLIB


# Gives the ability to suppress the version number : useful for Android bundling
AC_ARG_ENABLE([version],
							[AS_HELP_STRING([--enable-version], [embeds version info in the library [default=yes]])])

AM_CONDITIONAL(DISABLE_VERSION, test "x$enable_version" = "xno")

# Enables the bundling of czmq, uuid, zyre statically in the .so .
AC_ARG_ENABLE([embedded],
							[AS_HELP_STRING([--enable-embedded], [embeds zyre, uuid and czmq in the lib [default=no]])])

AC_ARG_ENABLE([embedded-zmq],
							[AS_HELP_STRING([--enable-embedded-zmq], [embeds zmq statically the lib [default=no]])])


# libuuid 
AC_ARG_WITH([libuuid],
            [AS_HELP_STRING([--with-libuuid],
						[Specify libuuid prefix])],
            [shuppan_search_libuuid="yes"],
            [])
if test "x$shuppan_search_libuuid" = "xyes"; then
    if test -r "${with_libuuid}/include/uuid/uuid.h"; then
        CPPFLAGS="-I${with_libuuid}/include ${CPPFLAGS}"
        LDFLAGS="-L${with_libuuid}/lib ${LDFLAGS}"
    else
        AC_MSG_ERROR([${with_libuuid}/include/uuid/uuid.h not found. Please check libuuid prefix])
    fi
fi

# libzmq 
AC_ARG_WITH([libzmq],
            [AS_HELP_STRING([--with-libzmq],
						[Specify libzmq prefix])],
            [shuppan_search_libzmq="yes"],
            [])
if test "x$shuppan_search_libzmq" = "xyes"; then
    if test -r "${with_libzmq}/include/zmq.h"; then
        CPPFLAGS="-I${with_libzmq}/include ${CPPFLAGS}"
        LDFLAGS="-L${with_libzmq}/lib ${LDFLAGS}"
    else
        AC_MSG_ERROR([${with_libzmq}/include/zmq.h not found. Please check libzmq prefix])
    fi
fi

# libczmq
AC_ARG_WITH([libczmq],
            [AS_HELP_STRING([--with-libczmq],
                            [Specify libczmq prefix])],
            [shuppan_search_libczmq="yes"],
            [])
if test "x$shuppan_search_libczmq" = "xyes"; then
    if test -r "${with_libczmq}/include/czmq.h"; then
        CPPFLAGS="-I${with_libczmq}/include ${CPPFLAGS}"
        LDFLAGS="-L${with_libczmq}/lib ${LDFLAGS}"
    else
        AC_MSG_ERROR([${with_libczmq}/include/czmq.h not found. Please check libczmq prefix])
    fi
fi

# libzre
AC_ARG_WITH([libzre],
            [AS_HELP_STRING([--with-libzre],
                            [Specify libzre prefix])],
            [shuppan_search_libzre="yes"],
            [])
if test "x$shuppan_search_libzre" = "xyes"; then
    if test -r "${with_libzre}/include/zre/zre.h"; then
        CPPFLAGS="-I${with_libzre}/include ${CPPFLAGS}"
        LDFLAGS="-L${with_libzre}/lib ${LDFLAGS}"
    else
        AC_MSG_ERROR([${with_libzre}/include/zre/zre.h not found. Please check libzre prefix])
    fi
fi

# Other libraries

AC_CHECK_LIB(uuid, uuid_generate, [
															 if test "x$enable_embedded" = "xyes"; then
																 LIBS="-Wl,-Bstatic -Wl,-luuid -Wl,-Bdynamic $LIBS"
															 else 
																 LIBS="-luuid $LIBS"
															 fi
															 ],
															 [AC_MSG_ERROR([cannot link with -luuid, install libuuid.])]
															 )

AC_CHECK_LIB(zmq, zmq_init, [
															 if test "x$enable_embedded_zmq" = "xyes"; then
																 LIBS="-Wl,-Bstatic -Wl,-lzmq -Wl,-Bdynamic -lrt -lpthread $LIBS"
															 else 
																 LIBS="-lzmq $LIBS"
															 fi
															 ],
															 [AC_MSG_ERROR([cannot link with -lzmq, install libzmq.])]
															 )

AC_CHECK_LIB(czmq, zctx_test, [
															 if test "x$enable_embedded" = "xyes"; then
																 LIBS="-Wl,-Bstatic -Wl,-lczmq -Wl,-Bdynamic $LIBS"
															 else 
																 LIBS="-lczmq $LIBS"
															 fi
															 ],
															 [AC_MSG_ERROR([cannot link with -lczmq, install libczmq.])]
															 )

AC_CHECK_LIB(zre, zre_msg_test, [
															 if test "x$enable_embedded" = "xyes"; then
																 LIBS="-Wl,-Bstatic -Wl,-lzre -Wl,-Bdynamic $LIBS"
															 else 
																 LIBS="-lzre $LIBS"
															 fi
															 ],
															 [AC_MSG_ERROR([cannot link with -lzre, install libzre.])]
															 )

# Host speciffic checks
AC_CANONICAL_HOST

# Determine whether or not documentation should be built and installed.


# Set some default features required by libzre code.
CPPFLAGS="-D_REENTRANT -D_THREAD_SAFE $CPPFLAGS"

# OS-specific tests
case "${host_os}" in
    *linux*)
        # Define on Linux to enable all library features
        CPPFLAGS="-D_GNU_SOURCE -DLINUX $CPPFLAGS"
        AC_DEFINE(LIBSHUPPAN_HAVE_LINUX, 1, [Have Linux OS])
        ;;
    *solaris*)
        # Define on Solaris to enable all library features
        CPPFLAGS="-D_PTHREADS $CPPFLAGS"
        AC_DEFINE(LIBSHUPPAN_HAVE_SOLARIS, 1, [Have Solaris OS])
        ;;
    *freebsd*)
        # Define on FreeBSD to enable all library features
        CPPFLAGS="-D__BSD_VISIBLE $CPPFLAGS"
        AC_DEFINE(LIBSHUPPAN_HAVE_FREEBSD, 1, [Have FreeBSD OS])
        ;;
    *darwin*)
        # Define on Darwin to enable all library features
        CPPFLAGS="-D_DARWIN_C_SOURCE $CPPFLAGS"
        AC_DEFINE(LIBSHUPPAN_HAVE_OSX, 1, [Have DarwinOSX OS])
        ;;
    *netbsd*)
        # Define on NetBSD to enable all library features
        CPPFLAGS="-D_NETBSD_SOURCE $CPPFLAGS"
        AC_DEFINE(LIBSHUPPAN_HAVE_NETBSD, 1, [Have NetBSD OS])
        ;;
    *openbsd*)
        # Define on OpenBSD to enable all library features
        CPPFLAGS="-D_BSD_SOURCE $CPPFLAGS"
        AC_DEFINE(LIBSHUPPAN_HAVE_OPENBSD, 1, [Have OpenBSD OS])
        ;;
    *nto-qnx*)
        AC_DEFINE(LIBSHUPPAN_HAVE_QNXNTO, 1, [Have QNX Neutrino OS])
        ;;
    *aix*)
        AC_DEFINE(LIBSHUPPAN_HAVE_AIX, 1, [Have AIX OS])
        ;;
    *hpux*)
        # Define on HP-UX to enable all library features
        CPPFLAGS="-D_POSIX_C_SOURCE=200112L"
        AC_DEFINE(LIBSHUPPAN_HAVE_HPUX, 1, [Have HPUX OS])
        ;;
    *mingw32*)
        AC_DEFINE(LIBSHUPPAN_HAVE_WINDOWS, 1, [Have Windows OS])
        AC_DEFINE(LIBSHUPPAN_HAVE_MINGW32, 1, [Have MinGW32])
        AC_CHECK_HEADERS(windows.h)
        shuppan_on_mingw32="yes"
        shuppan_install_man="no"
        ;;
    *cygwin*)
        # Define on Cygwin to enable all library features
        CPPFLAGS="-D_GNU_SOURCE $CPPFLAGS"
        AC_DEFINE(LIBSHUPPAN_HAVE_CYGWIN, 1, [Have Cygwin])
        ;;
    *)
        AC_MSG_ERROR([unsupported system: ${host_os}.])
        ;;
esac

# Checks for header files.
AC_HEADER_STDC
AC_HEADER_STDBOOL
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_TYPE_SSIZE_T
AC_HEADER_TIME
AC_TYPE_UINT32_T
AC_C_VOLATILE


# These options are GNU compiler specific.
if test "x$GCC" = "xyes"; then
    CPPFLAGS="-fPIC -Wno-write-strings -Wall -Wno-unused ${CPPFLAGS}"
		LDFLAGS="-fPIC ${LDFLAGS}"
fi
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

# Checks for library functions.
AC_TYPE_SIGNAL

AC_OUTPUT(Makefile src/Makefile src/libshuppan.pc)
